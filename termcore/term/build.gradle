plugins {
    id 'com.android.library'
}

android {
    namespace "com.termoneplus"

    compileSdk 34
    ndkVersion "24.0.8215888"
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 28

        vectorDrawables.useSupportLibrary = true

        versionCode 550
        versionName '5.5.0'

        manifestPlaceholders.applicationIdSuffix = ""
        buildConfigField("String", "APPLICATION_ID_SUFFIX", "\"\"")
        externalNativeBuild {
            cmake {
                arguments "-DAPPLICATION_ID:STRING=${applicationId}"
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    
    buildTypes {
        debug {
            buildConfigField("String", "APPLICATION_ID_SUFFIX", "\"${applicationIdSuffix}\"")
            externalNativeBuild {
                cmake {
                    arguments "-DAPPLICATION_ID:STRING=${defaultConfig.applicationId}${applicationIdSuffix}"
                }
            }
        }
        release {
            minifyEnabled false
        }
    }
    buildFeatures {
        aidl true
        buildConfig true
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }

}

dependencies {
    implementation project(':termcore:emulatorview')
    implementation project(':termcore:libtermexec')

    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.preference
    implementation libs.annotation
}

tasks.register('elfcleaner', Exec) {
    // only unix
    commandLine 'sh', '-c', 'find build/*/stripped* -name lib*.so -print0 | xargs -0 -P 0 elf-cleaner'
}

tasks.configureEach { task ->
    if (task.name.matches('strip.*DebugSymbols')) {
        task.finalizedBy elfcleaner
    }
}
